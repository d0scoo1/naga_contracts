// SPDX-License-Identifier: AGPL-3.0

    pragma solidity ^0.8.11;

    /// @title ClonesWithImmutableArgs
    /// @author wighawag, zefram.eth
    /// @notice Enables creating clone contracts with immutable args
    library ClonesWithImmutableArgs {
        error CreateFail();

        /// @notice Creates a clone proxy of the implementation contract, with immutable args
        /// @dev data cannot exceed 65535 bytes, since 2 bytes are used to store the data length
        /// @param implementation The implementation contract to clone
        /// @param data Encoded immutable args
        /// @return instance The address of the created clone
        function clone(address implementation, bytes memory data)
            internal
            returns (address instance)
        {
            // unrealistic for memory ptr or data length to exceed 256 bits
            unchecked {
                uint256 extraLength = data.length + 2; // +2 bytes for telling how much data there is appended to the call
                uint256 creationSize = 0x43 + extraLength;
                uint256 runSize = creationSize - 11;
                uint256 dataPtr;
                uint256 ptr;
                // solhint-disable-next-line no-inline-assembly
                assembly {
                    ptr := mload(0x40)

                    // -------------------------------------------------------------------------------------------------------------
                    // CREATION (11 bytes)
                    // -------------------------------------------------------------------------------------------------------------

                    // 3d          | RETURNDATASIZE        | 0                       | –
                    // 61 runtime  | PUSH2 runtime (r)     | r 0                     | –
                    mstore(
                        ptr,
                        0x3d61000000000000000000000000000000000000000000000000000000000000
                    )
                    mstore(add(ptr, 0x02), shl(240, runSize)) // size of the contract running bytecode (16 bits)

                    // creation size = 0b
                    // 80          | DUP1                  | r r 0                   | –
                    // 60 creation | PUSH1 creation (c)    | c r r 0                 | –
                    // 3d          | RETURNDATASIZE        | 0 c r r 0               | –
                    // 39          | CODECOPY              | r 0                     | [0-2d]: runtime code
                    // 81          | DUP2                  | 0 c  0                  | [0-2d]: runtime code
                    // f3          | RETURN                | 0                       | [0-2d]: runtime code
                    mstore(
                        add(ptr, 0x04),
                        0x80600b3d3981f300000000000000000000000000000000000000000000000000
                    )

                    // -------------------------------------------------------------------------------------------------------------
                    // RUNTIME
                    // -------------------------------------------------------------------------------------------------------------

                    // 36          | CALLDATASIZE          | cds                     | –
                    // 3d          | RETURNDATASIZE        | 0 cds                   | –
                    // 3d          | RETURNDATASIZE        | 0 0 cds                 | –
                    // 37          | CALLDATACOPY          | –                       | [0, cds] = calldata
                    // 61          | PUSH2 extra           | extra                   | [0, cds] = calldata
                    mstore(
                        add(ptr, 0x0b),
                        0x363d3d3761000000000000000000000000000000000000000000000000000000
                    )
                    mstore(add(ptr, 0x10), shl(240, extraLength))

                    // 60 0x38     | PUSH1 0x38            | 0x38 extra              | [0, cds] = calldata // 0x38 (56) is runtime size - data
                    // 36          | CALLDATASIZE          | cds 0x38 extra          | [0, cds] = calldata
                    // 39          | CODECOPY              | _                       | [0, cds] = calldata
                    // 3d          | RETURNDATASIZE        | 0                       | [0, cds] = calldata
                    // 3d          | RETURNDATASIZE        | 0 0                     | [0, cds] = calldata
                    // 3d          | RETURNDATASIZE        | 0 0 0                   | [0, cds] = calldata
                    // 36          | CALLDATASIZE          | cds 0 0 0               | [0, cds] = calldata
                    // 61 extra    | PUSH2 extra           | extra cds 0 0 0         | [0, cds] = calldata
                    mstore(
                        add(ptr, 0x12),
                        0x603836393d3d3d36610000000000000000000000000000000000000000000000
                    )
                    mstore(add(ptr, 0x1b), shl(240, extraLength))

                    // 01          | ADD                   | cds+extra 0 0 0         | [0, cds] = calldata
                    // 3d          | RETURNDATASIZE        | 0 cds 0 0 0             | [0, cds] = calldata
                    // 73 addr     | PUSH20 0x123…         | addr 0 cds 0 0 0        | [0, cds] = calldata
                    mstore(
                        add(ptr, 0x1d),
                        0x013d730000000000000000000000000000000000000000000000000000000000
                    )
                    mstore(add(ptr, 0x20), shl(0x60, implementation))

                    // 5a          | GAS                   | gas addr 0 cds 0 0 0    | [0, cds] = calldata
                    // f4          | DELEGATECALL          | success 0               | [0, cds] = calldata
                    // 3d          | RETURNDATASIZE        | rds success 0           | [0, cds] = calldata
                    // 82          | DUP3                  | 0 rds success 0         | [0, cds] = calldata
                    // 80          | DUP1                  | 0 0 rds success 0       | [0, cds] = calldata
                    // 3e          | RETURNDATACOPY        | success 0               | [0, rds] = return data (there might be some irrelevant leftovers in memory [rds, cds] when rds < cds)
                    // 90          | SWAP1                 | 0 success               | [0, rds] = return data
                    // 3d          | RETURNDATASIZE        | rds 0 success           | [0, rds] = return data
                    // 91          | SWAP2                 | success 0 rds           | [0, rds] = return data
                    // 60 0x36     | PUSH1 0x36            | 0x36 sucess 0 rds       | [0, rds] = return data
                    // 57          | JUMPI                 | 0 rds                   | [0, rds] = return data
                    // fd          | REVERT                | –                       | [0, rds] = return data
                    // 5b          | JUMPDEST              | 0 rds                   | [0, rds] = return data
                    // f3          | RETURN                | –                       | [0, rds] = return data

                    mstore(
                        add(ptr, 0x34),
                        0x5af43d82803e903d91603657fd5bf30000000000000000000000000000000000
                    )
                }

                // -------------------------------------------------------------------------------------------------------------
                // APPENDED DATA (Accessible from extcodecopy)
                // (but also send as appended data to the delegatecall)
                // -------------------------------------------------------------------------------------------------------------

                extraLength -= 2;
                uint256 counter = extraLength;
                uint256 copyPtr = ptr + 0x43;
                // solhint-disable-next-line no-inline-assembly
                assembly {
                    dataPtr := add(data, 32)
                }
                for (; counter >= 32; counter -= 32) {
                    // solhint-disable-next-line no-inline-assembly
                    assembly {
                        mstore(copyPtr, mload(dataPtr))
                    }

                    copyPtr += 32;
                    dataPtr += 32;
                }
                uint256 mask = ~(256**(32 - counter) - 1);
                // solhint-disable-next-line no-inline-assembly
                assembly {
                    mstore(copyPtr, and(mload(dataPtr), mask))
                }
                copyPtr += counter;
                // solhint-disable-next-line no-inline-assembly
                assembly {
                    mstore(copyPtr, shl(240, extraLength))
                }
                // solhint-disable-next-line no-inline-assembly
                assembly {
                    instance := create(0, ptr, creationSize)
                }
                if (instance == address(0)) {
                    revert CreateFail();
                }
            }
        }
    }


    /// @title Clone
    /// @author zefram.eth
    /// @notice Provides helper functions for reading immutable args from calldata
    contract Clone {
        /// @notice Reads an immutable arg with type address
        /// @param argOffset The offset of the arg in the packed data
        /// @return arg The arg value
        function _getArgAddress(uint256 argOffset)
            internal
            pure
            returns (address arg)
        {
            uint256 offset = _getImmutableArgsOffset();
            assembly {
                arg := shr(0x60, calldataload(add(offset, argOffset)))
            }
        }

        /// @notice Reads an immutable arg with type uint256
        /// @param argOffset The offset of the arg in the packed data
        /// @return arg The arg value
        function _getArgUint256(uint256 argOffset)
            internal
            pure
            returns (uint256 arg)
        {
            uint256 offset = _getImmutableArgsOffset();
            // solhint-disable-next-line no-inline-assembly
            assembly {
                arg := calldataload(add(offset, argOffset))
            }
        }

        /// @notice Reads an immutable arg with type uint64
        /// @param argOffset The offset of the arg in the packed data
        /// @return arg The arg value
        function _getArgUint64(uint256 argOffset)
            internal
            pure
            returns (uint64 arg)
        {
            uint256 offset = _getImmutableArgsOffset();
            // solhint-disable-next-line no-inline-assembly
            assembly {
                arg := shr(0xc0, calldataload(add(offset, argOffset)))
            }
        }

        /// @notice Reads an immutable arg with type uint8
        /// @param argOffset The offset of the arg in the packed data
        /// @return arg The arg value
        function _getArgUint8(uint256 argOffset) internal pure returns (uint8 arg) {
            uint256 offset = _getImmutableArgsOffset();
            // solhint-disable-next-line no-inline-assembly
            assembly {
                arg := shr(0xf8, calldataload(add(offset, argOffset)))
            }
        }

        /// @return offset The offset of the packed immutable args in calldata
        function _getImmutableArgsOffset() internal pure returns (uint256 offset) {
            // solhint-disable-next-line no-inline-assembly
            assembly {
                offset := sub(
                    calldatasize(),
                    add(shr(240, calldataload(sub(calldatasize(), 2))), 2)
                )
            }
        }
    }

    /// @notice Modern and gas efficient ERC20 + EIP-2612 implementation.
    /// @author Solmate (https://github.com/Rari-Capital/solmate/blob/main/src/tokens/ERC20.sol)
    /// @author Modified from Uniswap (https://github.com/Uniswap/uniswap-v2-core/blob/master/contracts/UniswapV2ERC20.sol)
    /// @dev Do not manually set balances without updating totalSupply, as the sum of all user balances must not exceed it.
    abstract contract ERC20Clone is Clone {
        /*///////////////////////////////////////////////////////////////
                                    EVENTS
        //////////////////////////////////////////////////////////////*/

        event Transfer(address indexed from, address indexed to, uint256 amount);

        event Approval(
            address indexed owner,
            address indexed spender,
            uint256 amount
        );

        /*///////////////////////////////////////////////////////////////
                                ERC20 STORAGE
        //////////////////////////////////////////////////////////////*/

        uint256 public totalSupply;

        mapping(address => uint256) public balanceOf;

        mapping(address => mapping(address => uint256)) public allowance;

        /*///////////////////////////////////////////////////////////////
                                METADATA
        //////////////////////////////////////////////////////////////*/

        function name() external pure returns (string memory) {
            return string(abi.encodePacked(_getArgUint256(0)));
        }

        function symbol() external pure returns (string memory) {
            return string(abi.encodePacked(_getArgUint256(0x20)));
        }

        function decimals() external pure returns (uint8) {
            return _getArgUint8(0x40);
        }

        /*///////////////////////////////////////////////////////////////
                                ERC20 LOGIC
        //////////////////////////////////////////////////////////////*/

        function approve(address spender, uint256 amount)
            public
            virtual
            returns (bool)
        {
            allowance[msg.sender][spender] = amount;

            emit Approval(msg.sender, spender, amount);

            return true;
        }

        function transfer(address to, uint256 amount)
            public
            virtual
            returns (bool)
        {
            balanceOf[msg.sender] -= amount;

            // Cannot overflow because the sum of all user
            // balances can't exceed the max uint256 value.
            unchecked {
                balanceOf[to] += amount;
            }

            emit Transfer(msg.sender, to, amount);

            return true;
        }

        function transferFrom(
            address from,
            address to,
            uint256 amount
        ) public virtual returns (bool) {
            uint256 allowed = allowance[from][msg.sender]; // Saves gas for limited approvals.

            if (allowed != type(uint256).max)
                allowance[from][msg.sender] = allowed - amount;

            balanceOf[from] -= amount;

            // Cannot overflow because the sum of all user
            // balances can't exceed the max uint256 value.
            unchecked {
                balanceOf[to] += amount;
            }

            emit Transfer(from, to, amount);

            return true;
        }

        /*///////////////////////////////////////////////////////////////
                        INTERNAL LOGIC
        //////////////////////////////////////////////////////////////*/

        function _mint(address to, uint256 amount) internal virtual {
            totalSupply += amount;

            // Cannot overflow because the sum of all user
            // balances can't exceed the max uint256 value.
            unchecked {
                balanceOf[to] += amount;
            }

            emit Transfer(address(0), to, amount);
        }

        function _burn(address from, uint256 amount) internal virtual {
            balanceOf[from] -= amount;

            // Cannot underflow because a user's balance
            // will never be larger than the total supply.
            unchecked {
                totalSupply -= amount;
            }

            emit Transfer(from, address(0), amount);
        }

        function _getImmutableVariablesOffset()
            internal
            pure
            returns (uint256 offset)
        {
            assembly {
                offset := sub(
                    calldatasize(),
                    add(shr(240, calldataload(sub(calldatasize(), 2))), 2)
                )
            }
        }
    }


    /// @notice Modern and gas efficient ERC20 + EIP-2612 implementation.
    /// @author Solmate (https://github.com/Rari-Capital/solmate/blob/main/src/tokens/ERC20.sol)
    /// @author Modified from Uniswap (https://github.com/Uniswap/uniswap-v2-core/blob/master/contracts/UniswapV2ERC20.sol)
    /// @dev Do not manually set balances without updating totalSupply, as the sum of all user balances must not exceed it.
    abstract contract ERC20 {
        /*///////////////////////////////////////////////////////////////
                                    EVENTS
        //////////////////////////////////////////////////////////////*/

        event Transfer(address indexed from, address indexed to, uint256 amount);

        event Approval(address indexed owner, address indexed spender, uint256 amount);

        /*///////////////////////////////////////////////////////////////
                                METADATA STORAGE
        //////////////////////////////////////////////////////////////*/

        string public name;

        string public symbol;

        uint8 public immutable decimals;

        /*///////////////////////////////////////////////////////////////
                                ERC20 STORAGE
        //////////////////////////////////////////////////////////////*/

        uint256 public totalSupply;

        mapping(address => uint256) public balanceOf;

        mapping(address => mapping(address => uint256)) public allowance;

        /*///////////////////////////////////////////////////////////////
                                EIP-2612 STORAGE
        //////////////////////////////////////////////////////////////*/

        bytes32 public constant PERMIT_TYPEHASH =
            keccak256("Permit(address owner,address spender,uint256 value,uint256 nonce,uint256 deadline)");

        uint256 internal immutable INITIAL_CHAIN_ID;

        bytes32 internal immutable INITIAL_DOMAIN_SEPARATOR;

        mapping(address => uint256) public nonces;

        /*///////////////////////////////////////////////////////////////
                                CONSTRUCTOR
        //////////////////////////////////////////////////////////////*/

        constructor(
            string memory _name,
            string memory _symbol,
            uint8 _decimals
        ) {
            name = _name;
            symbol = _symbol;
            decimals = _decimals;

            INITIAL_CHAIN_ID = block.chainid;
            INITIAL_DOMAIN_SEPARATOR = computeDomainSeparator();
        }

        /*///////////////////////////////////////////////////////////////
                                ERC20 LOGIC
        //////////////////////////////////////////////////////////////*/

        function approve(address spender, uint256 amount) public virtual returns (bool) {
            allowance[msg.sender][spender] = amount;

            emit Approval(msg.sender, spender, amount);

            return true;
        }

        function transfer(address to, uint256 amount) public virtual returns (bool) {
            balanceOf[msg.sender] -= amount;

            // Cannot overflow because the sum of all user
            // balances can't exceed the max uint256 value.
            unchecked {
                balanceOf[to] += amount;
            }

            emit Transfer(msg.sender, to, amount);

            return true;
        }

        function transferFrom(
            address from,
            address to,
            uint256 amount
        ) public virtual returns (bool) {
            uint256 allowed = allowance[from][msg.sender]; // Saves gas for limited approvals.

            if (allowed != type(uint256).max) allowance[from][msg.sender] = allowed - amount;

            balanceOf[from] -= amount;

            // Cannot overflow because the sum of all user
            // balances can't exceed the max uint256 value.
            unchecked {
                balanceOf[to] += amount;
            }

            emit Transfer(from, to, amount);

            return true;
        }

        /*///////////////////////////////////////////////////////////////
                                EIP-2612 LOGIC
        //////////////////////////////////////////////////////////////*/

        function permit(
            address owner,
            address spender,
            uint256 value,
            uint256 deadline,
            uint8 v,
            bytes32 r,
            bytes32 s
        ) public virtual {
            require(deadline >= block.timestamp, "PERMIT_DEADLINE_EXPIRED");

            // Unchecked because the only math done is incrementing
            // the owner's nonce which cannot realistically overflow.
            unchecked {
                bytes32 digest = keccak256(
                    abi.encodePacked(
                        "\x19\x01",
                        DOMAIN_SEPARATOR(),
                        keccak256(abi.encode(PERMIT_TYPEHASH, owner, spender, value, nonces[owner]++, deadline))
                    )
                );

                address recoveredAddress = ecrecover(digest, v, r, s);

                require(recoveredAddress != address(0) && recoveredAddress == owner, "INVALID_SIGNER");

                allowance[recoveredAddress][spender] = value;
            }

            emit Approval(owner, spender, value);
        }

        function DOMAIN_SEPARATOR() public view virtual returns (bytes32) {
            return block.chainid == INITIAL_CHAIN_ID ? INITIAL_DOMAIN_SEPARATOR : computeDomainSeparator();
        }

        function computeDomainSeparator() internal view virtual returns (bytes32) {
            return
                keccak256(
                    abi.encode(
                        keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"),
                        keccak256(bytes(name)),
                        keccak256("1"),
                        block.chainid,
                        address(this)
                    )
                );
        }

        /*///////////////////////////////////////////////////////////////
                        INTERNAL MINT/BURN LOGIC
        //////////////////////////////////////////////////////////////*/

        function _mint(address to, uint256 amount) internal virtual {
            totalSupply += amount;

            // Cannot overflow because the sum of all user
            // balances can't exceed the max uint256 value.
            unchecked {
                balanceOf[to] += amount;
            }

            emit Transfer(address(0), to, amount);
        }

        function _burn(address from, uint256 amount) internal virtual {
            balanceOf[from] -= amount;

            // Cannot underflow because a user's balance
            // will never be larger than the total supply.
            unchecked {
                totalSupply -= amount;
            }

            emit Transfer(from, address(0), amount);
        }
    }


    /// @notice Safe ETH and ERC20 transfer library that gracefully handles missing return values.
    /// @author Solmate (https://github.com/Rari-Capital/solmate/blob/main/src/utils/SafeTransferLib.sol)
    /// @author Modified from Gnosis (https://github.com/gnosis/gp-v2-contracts/blob/main/src/contracts/libraries/GPv2SafeERC20.sol)
    /// @dev Use with caution! Some functions in this library knowingly create dirty bits at the destination of the free memory pointer.
    library SafeTransferLib {
        /*///////////////////////////////////////////////////////////////
                                ETH OPERATIONS
        //////////////////////////////////////////////////////////////*/

        function safeTransferETH(address to, uint256 amount) internal {
            bool callStatus;

            assembly {
                // Transfer the ETH and store if it succeeded or not.
                callStatus := call(gas(), to, amount, 0, 0, 0, 0)
            }

            require(callStatus, "ETH_TRANSFER_FAILED");
        }

        /*///////////////////////////////////////////////////////////////
                            ERC20 OPERATIONS
        //////////////////////////////////////////////////////////////*/

        function safeTransferFrom(
            ERC20 token,
            address from,
            address to,
            uint256 amount
        ) internal {
            bool callStatus;

            assembly {
                // Get a pointer to some free memory.
                let freeMemoryPointer := mload(0x40)

                // Write the abi-encoded calldata to memory piece by piece:
                mstore(freeMemoryPointer, 0x23b872dd00000000000000000000000000000000000000000000000000000000) // Begin with the function selector.
                mstore(add(freeMemoryPointer, 4), and(from, 0xffffffffffffffffffffffffffffffffffffffff)) // Mask and append the "from" argument.
                mstore(add(freeMemoryPointer, 36), and(to, 0xffffffffffffffffffffffffffffffffffffffff)) // Mask and append the "to" argument.
                mstore(add(freeMemoryPointer, 68), amount) // Finally append the "amount" argument. No mask as it's a full 32 byte value.

                // Call the token and store if it succeeded or not.
                // We use 100 because the calldata length is 4 + 32 * 3.
                callStatus := call(gas(), token, 0, freeMemoryPointer, 100, 0, 0)
            }

            require(didLastOptionalReturnCallSucceed(callStatus), "TRANSFER_FROM_FAILED");
        }

        function safeTransfer(
            ERC20 token,
            address to,
            uint256 amount
        ) internal {
            bool callStatus;

            assembly {
                // Get a pointer to some free memory.
                let freeMemoryPointer := mload(0x40)

                // Write the abi-encoded calldata to memory piece by piece:
                mstore(freeMemoryPointer, 0xa9059cbb00000000000000000000000000000000000000000000000000000000) // Begin with the function selector.
                mstore(add(freeMemoryPointer, 4), and(to, 0xffffffffffffffffffffffffffffffffffffffff)) // Mask and append the "to" argument.
                mstore(add(freeMemoryPointer, 36), amount) // Finally append the "amount" argument. No mask as it's a full 32 byte value.

                // Call the token and store if it succeeded or not.
                // We use 68 because the calldata length is 4 + 32 * 2.
                callStatus := call(gas(), token, 0, freeMemoryPointer, 68, 0, 0)
            }

            require(didLastOptionalReturnCallSucceed(callStatus), "TRANSFER_FAILED");
        }

        function safeApprove(
            ERC20 token,
            address to,
            uint256 amount
        ) internal {
            bool callStatus;

            assembly {
                // Get a pointer to some free memory.
                let freeMemoryPointer := mload(0x40)

                // Write the abi-encoded calldata to memory piece by piece:
                mstore(freeMemoryPointer, 0x095ea7b300000000000000000000000000000000000000000000000000000000) // Begin with the function selector.
                mstore(add(freeMemoryPointer, 4), and(to, 0xffffffffffffffffffffffffffffffffffffffff)) // Mask and append the "to" argument.
                mstore(add(freeMemoryPointer, 36), amount) // Finally append the "amount" argument. No mask as it's a full 32 byte value.

                // Call the token and store if it succeeded or not.
                // We use 68 because the calldata length is 4 + 32 * 2.
                callStatus := call(gas(), token, 0, freeMemoryPointer, 68, 0, 0)
            }

            require(didLastOptionalReturnCallSucceed(callStatus), "APPROVE_FAILED");
        }

        /*///////////////////////////////////////////////////////////////
                            INTERNAL HELPER LOGIC
        //////////////////////////////////////////////////////////////*/

        function didLastOptionalReturnCallSucceed(bool callStatus) private pure returns (bool success) {
            assembly {
                // Get how many bytes the call returned.
                let returnDataSize := returndatasize()

                // If the call reverted:
                if iszero(callStatus) {
                    // Copy the revert message into memory.
                    returndatacopy(0, 0, returnDataSize)

                    // Revert with the same message.
                    revert(0, returnDataSize)
                }

                switch returnDataSize
                case 32 {
                    // Copy the return data into memory.
                    returndatacopy(0, 0, returnDataSize)

                    // Set success to whether it returned true.
                    success := iszero(iszero(mload(0)))
                }
                case 0 {
                    // There was no return data.
                    success := 1
                }
                default {
                    // It returned some malformed input.
                    success := 0
                }
            }
        }
    }




    /// @title Contains 512-bit math functions
    /// @notice Facilitates multiplication and division that can have overflow of an intermediate value without any loss of precision
    /// @dev Handles "phantom overflow" i.e., allows multiplication and division where an intermediate value overflows 256 bits
    library FullMath {
        /// @notice Calculates floor(a×b÷denominator) with full precision. Throws if result overflows a uint256 or denominator == 0
        /// @param a The multiplicand
        /// @param b The multiplier
        /// @param denominator The divisor
        /// @return result The 256-bit result
        /// @dev Credit to Remco Bloemen under MIT license https://xn--2-umb.com/21/muldiv
        function mulDiv(
            uint256 a,
            uint256 b,
            uint256 denominator
        ) internal pure returns (uint256 result) {
            unchecked {
                // 512-bit multiply [prod1 prod0] = a * b
                // Compute the product mod 2**256 and mod 2**256 - 1
                // then use the Chinese Remainder Theorem to reconstruct
                // the 512 bit result. The result is stored in two 256
                // variables such that product = prod1 * 2**256 + prod0
                uint256 prod0; // Least significant 256 bits of the product
                uint256 prod1; // Most significant 256 bits of the product
                assembly {
                    let mm := mulmod(a, b, not(0))
                    prod0 := mul(a, b)
                    prod1 := sub(sub(mm, prod0), lt(mm, prod0))
                }

                // Handle non-overflow cases, 256 by 256 division
                if (prod1 == 0) {
                    require(denominator > 0);
                    assembly {
                        result := div(prod0, denominator)
                    }
                    return result;
                }

                // Make sure the result is less than 2**256.
                // Also prevents denominator == 0
                require(denominator > prod1);

                ///////////////////////////////////////////////
                // 512 by 256 division.
                ///////////////////////////////////////////////

                // Make division exact by subtracting the remainder from [prod1 prod0]
                // Compute remainder using mulmod
                uint256 remainder;
                assembly {
                    remainder := mulmod(a, b, denominator)
                }
                // Subtract 256 bit number from 512 bit number
                assembly {
                    prod1 := sub(prod1, gt(remainder, prod0))
                    prod0 := sub(prod0, remainder)
                }

                // Factor powers of two out of denominator
                // Compute largest power of two divisor of denominator.
                // Always >= 1.
                uint256 twos = (type(uint256).max - denominator + 1) & denominator;
                // Divide denominator by power of two
                assembly {
                    denominator := div(denominator, twos)
                }

                // Divide [prod1 prod0] by the factors of two
                assembly {
                    prod0 := div(prod0, twos)
                }
                // Shift in bits from prod1 into prod0. For this we need
                // to flip `twos` such that it is 2**256 / twos.
                // If twos is zero, then it becomes one
                assembly {
                    twos := add(div(sub(0, twos), twos), 1)
                }
                prod0 |= prod1 * twos;

                // Invert denominator mod 2**256
                // Now that denominator is an odd number, it has an inverse
                // modulo 2**256 such that denominator * inv = 1 mod 2**256.
                // Compute the inverse by starting with a seed that is correct
                // correct for four bits. That is, denominator * inv = 1 mod 2**4
                uint256 inv = (3 * denominator) ^ 2;
                // Now use Newton-Raphson iteration to improve the precision.
                // Thanks to Hensel's lifting lemma, this also works in modular
                // arithmetic, doubling the correct bits in each step.
                inv *= 2 - denominator * inv; // inverse mod 2**8
                inv *= 2 - denominator * inv; // inverse mod 2**16
                inv *= 2 - denominator * inv; // inverse mod 2**32
                inv *= 2 - denominator * inv; // inverse mod 2**64
                inv *= 2 - denominator * inv; // inverse mod 2**128
                inv *= 2 - denominator * inv; // inverse mod 2**256

                // Because the division is now exact we can divide by multiplying
                // with the modular inverse of denominator. This will give us the
                // correct result modulo 2**256. Since the precoditions guarantee
                // that the outcome is less than 2**256, this is the final result.
                // We don't need to compute the high bits of the result and prod1
                // is no longer required.
                result = prod0 * inv;
                return result;
            }
        }

        /// @notice Calculates ceil(a×b÷denominator) with full precision. Throws if result overflows a uint256 or denominator == 0
        /// @param a The multiplicand
        /// @param b The multiplier
        /// @param denominator The divisor
        /// @return result The 256-bit result
        function mulDivRoundingUp(
            uint256 a,
            uint256 b,
            uint256 denominator
        ) internal pure returns (uint256 result) {
            result = mulDiv(a, b, denominator);
            unchecked {
                if (mulmod(a, b, denominator) > 0) {
                    require(result < type(uint256).max);
                    result++;
                }
            }
        }
    }

    /// @title VestedERC20
    /// @author zefram.eth
    /// @notice An ERC20 wrapper token that linearly vests an underlying token to
    /// its holders
    contract VestedERC20 is ERC20Clone {
        /// -----------------------------------------------------------------------
        /// Library usage
        /// -----------------------------------------------------------------------

        using SafeTransferLib for ERC20;

        /// -----------------------------------------------------------------------
        /// Errors
        /// -----------------------------------------------------------------------

        error Error_Wrap_VestOver();
        error Error_Wrap_AmountTooLarge();

        /// -----------------------------------------------------------------------
        /// Storage variables
        /// -----------------------------------------------------------------------

        /// @notice The amount of underlying tokens claimed by a token holder
        mapping(address => uint256) public claimedUnderlyingAmount;

        /// -----------------------------------------------------------------------
        /// Immutable parameters
        /// -----------------------------------------------------------------------

        /// @notice The token that is vested
        /// @return _underlying The address of the underlying token
        function underlying() public pure returns (address _underlying) {
            return _getArgAddress(0x41);
        }

        /// @notice The Unix timestamp (in seconds) of the start of the vest
        /// @return _startTimestamp The vest start timestamp
        function startTimestamp() public pure returns (uint64 _startTimestamp) {
            return _getArgUint64(0x55);
        }

        /// @notice The Unix timestamp (in seconds) of the end of the vest
        /// @return _endTimestamp The vest end timestamp
        function endTimestamp() public pure returns (uint64 _endTimestamp) {
            return _getArgUint64(0x5d);
        }

        /// -----------------------------------------------------------------------
        /// User actions
        /// -----------------------------------------------------------------------

        /// @notice Mints wrapped tokens using underlying tokens. Can only be called before the vest is over.
        /// @param underlyingAmount The amount of underlying tokens to wrap
        /// @param recipient The address that will receive the minted wrapped tokens
        /// @return wrappedTokenAmount The amount of wrapped tokens minted
        function wrap(uint256 underlyingAmount, address recipient)
            external
            returns (uint256 wrappedTokenAmount)
        {
            /// -------------------------------------------------------------------
            /// Validation
            /// -------------------------------------------------------------------

            uint256 _startTimestamp = startTimestamp();
            uint256 _endTimestamp = endTimestamp();
            if (block.timestamp >= _endTimestamp) {
                revert Error_Wrap_VestOver();
            }
            if (
                underlyingAmount >=
                type(uint256).max / (_endTimestamp - _startTimestamp)
            ) {
                revert Error_Wrap_AmountTooLarge();
            }

            /// -------------------------------------------------------------------
            /// State updates
            /// -------------------------------------------------------------------

            if (block.timestamp >= _startTimestamp) {
                // vest already started
                // wrappedTokenAmount * (endTimestamp() - block.timestamp) / (endTimestamp() - startTimestamp()) == underlyingAmount
                // thus, wrappedTokenAmount = underlyingAmount * (endTimestamp() - startTimestamp()) / (endTimestamp() - block.timestamp)
                wrappedTokenAmount =
                    (underlyingAmount * (_endTimestamp - _startTimestamp)) /
                    (_endTimestamp - block.timestamp);

                // pretend we have claimed the vested underlying amount
                claimedUnderlyingAmount[recipient] +=
                    wrappedTokenAmount -
                    underlyingAmount;
            } else {
                // vest hasn't started yet
                wrappedTokenAmount = underlyingAmount;
            }
            // mint wrapped tokens
            _mint(recipient, wrappedTokenAmount);

            /// -------------------------------------------------------------------
            /// Effects
            /// -------------------------------------------------------------------

            ERC20 underlyingToken = ERC20(underlying());
            underlyingToken.safeTransferFrom(
                msg.sender,
                address(this),
                underlyingAmount
            );
        }

        /// @notice Allows a holder of the wrapped token to redeem the vested tokens
        /// @param recipient The address that will receive the vested tokens
        /// @return redeemedAmount The amount of vested tokens redeemed
        function redeem(address recipient)
            external
            returns (uint256 redeemedAmount)
        {
            /// -------------------------------------------------------------------
            /// State updates
            /// -------------------------------------------------------------------

            uint256 _claimedUnderlyingAmount = claimedUnderlyingAmount[msg.sender];
            redeemedAmount = _getRedeemableAmount(
                msg.sender,
                _claimedUnderlyingAmount
            );
            claimedUnderlyingAmount[msg.sender] =
                _claimedUnderlyingAmount +
                redeemedAmount;

            /// -------------------------------------------------------------------
            /// Effects
            /// -------------------------------------------------------------------

            if (redeemedAmount > 0) {
                ERC20 underlyingToken = ERC20(underlying());
                underlyingToken.safeTransfer(recipient, redeemedAmount);
            }
        }

        /// @notice The ERC20 transfer function
        function transfer(address to, uint256 amount)
            public
            override
            returns (bool)
        {
            uint256 senderBalance = balanceOf[msg.sender];
            uint256 senderClaimedUnderlyingAmount = claimedUnderlyingAmount[
                msg.sender
            ];

            balanceOf[msg.sender] = senderBalance - amount;

            // Cannot overflow because the sum of all user
            // balances can't exceed the max uint256 value.
            unchecked {
                balanceOf[to] += amount;
            }

            uint256 claimedUnderlyingAmountToTransfer = FullMath.mulDiv(
                senderClaimedUnderlyingAmount,
                amount,
                senderBalance
            );

            if (claimedUnderlyingAmountToTransfer > 0) {
                claimedUnderlyingAmount[msg.sender] =
                    senderClaimedUnderlyingAmount -
                    claimedUnderlyingAmountToTransfer;
                unchecked {
                    claimedUnderlyingAmount[
                        to
                    ] += claimedUnderlyingAmountToTransfer;
                }
            }

            emit Transfer(msg.sender, to, amount);

            return true;
        }

        /// @notice The ERC20 transferFrom function
        function transferFrom(
            address from,
            address to,
            uint256 amount
        ) public override returns (bool) {
            uint256 allowed = allowance[from][msg.sender]; // Saves gas for limited approvals.

            if (allowed != type(uint256).max)
                allowance[from][msg.sender] = allowed - amount;

            uint256 fromBalance = balanceOf[from];
            uint256 fromClaimedUnderlyingAmount = claimedUnderlyingAmount[from];

            balanceOf[from] = fromBalance - amount;

            // Cannot overflow because the sum of all user
            // balances can't exceed the max uint256 value.
            unchecked {
                balanceOf[to] += amount;
            }

            uint256 claimedUnderlyingAmountToTransfer = FullMath.mulDiv(
                fromClaimedUnderlyingAmount,
                amount,
                fromBalance
            );
            if (claimedUnderlyingAmountToTransfer > 0) {
                claimedUnderlyingAmount[from] =
                    fromClaimedUnderlyingAmount -
                    claimedUnderlyingAmountToTransfer;
                unchecked {
                    claimedUnderlyingAmount[
                        to
                    ] += claimedUnderlyingAmountToTransfer;
                }
            }

            emit Transfer(from, to, amount);

            return true;
        }

        /// -----------------------------------------------------------------------
        /// Getters
        /// -----------------------------------------------------------------------

        /// @notice Computes the amount of vested tokens redeemable by an account
        /// @param holder The wrapped token holder to query
        /// @return The amount of vested tokens redeemable
        function getRedeemableAmount(address holder)
            external
            view
            returns (uint256)
        {
            return _getRedeemableAmount(holder, claimedUnderlyingAmount[holder]);
        }

        /// -----------------------------------------------------------------------
        /// Internal functions
        /// -----------------------------------------------------------------------

        function _getRedeemableAmount(
            address holder,
            uint256 holderClaimedUnderlyingAmount
        ) internal view returns (uint256) {
            uint256 _startTimestamp = startTimestamp();
            uint256 _endTimestamp = endTimestamp();
            if (block.timestamp <= _startTimestamp) {
                // vest hasn't started yet, nothing is vested
                return 0;
            } else if (block.timestamp >= _endTimestamp) {
                // vest is over, everything is vested
                return balanceOf[holder] - holderClaimedUnderlyingAmount;
            } else {
                // middle of vest, compute linear vesting
                return
                    (balanceOf[holder] * (block.timestamp - _startTimestamp)) /
                    (_endTimestamp - _startTimestamp) -
                    holderClaimedUnderlyingAmount;
            }
        }
    }

    /// @title VestedERC20Factory
    /// @author zefram.eth
    /// @notice Factory for deploying VestedERC20 contracts cheaply
    contract VestedERC20Factory {
        /// -----------------------------------------------------------------------
        /// Library usage
        /// -----------------------------------------------------------------------

        using ClonesWithImmutableArgs for address;

        /// -----------------------------------------------------------------------
        /// Errors
        /// -----------------------------------------------------------------------

        error Error_InvalidTimeRange();

        /// -----------------------------------------------------------------------
        /// Events
        /// -----------------------------------------------------------------------

        event CreateVestedERC20(VestedERC20 vestedERC20);

        /// -----------------------------------------------------------------------
        /// Immutable parameters
        /// -----------------------------------------------------------------------

        /// @notice The VestedERC20 used as the template for all clones created
        VestedERC20 public immutable implementation;

        constructor(VestedERC20 implementation_) {
            implementation = implementation_;
        }

        /// @notice Creates a VestedERC20 contract
        /// @dev Uses a modified minimal proxy contract that stores immutable parameters in code and
        /// passes them in through calldata. See ClonesWithCallData.
        /// @param name The name of the VestedERC20 token
        /// @param symbol The symbol of the VestedERC20 token
        /// @param decimals The number of decimals used by the VestedERC20 token
        /// @param underlying The ERC20 token that is vested
        /// @param startTimestamp The start time of the vest, Unix timestamp in seconds
        /// @param endTimestamp The end time of the vest, must be greater than startTimestamp, Unix timestamp in seconds
        /// @return vestedERC20 The created VestedERC20 contract
        function createVestedERC20(
            bytes32 name,
            bytes32 symbol,
            uint8 decimals,
            address underlying,
            uint64 startTimestamp,
            uint64 endTimestamp
        ) external returns (VestedERC20 vestedERC20) {
            if (endTimestamp <= startTimestamp) {
                revert Error_InvalidTimeRange();
            }
            bytes memory data = abi.encodePacked(
                name,
                symbol,
                decimals,
                underlying,
                startTimestamp,
                endTimestamp
            );
            vestedERC20 = VestedERC20(address(implementation).clone(data));
            emit CreateVestedERC20(vestedERC20);
        }
    }