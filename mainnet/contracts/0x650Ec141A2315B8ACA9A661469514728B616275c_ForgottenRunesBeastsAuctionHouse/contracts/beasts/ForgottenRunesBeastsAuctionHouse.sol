// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@%@@@@@@@@@@@@@@@@@@@@@@(((        ((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                 ( @@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@((                             (@@@@@            ,         @@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@         @@@@@@@@@@@@@@@@@@@@@@       (((    ((((((      ((((((  @@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@((     (((@@@@@@@@@@@@@@@@@@@@@@@@@@(,((,       ,,,,,       ((((%(%(,(@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@       @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((                       (((@@@@(((   @@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@       (@@@@@@@@@@@@@@@@@@@@@@(( @@@@@@@( , ,(   ,  ((   ,           ,,(((((((((, @@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@       @@@@@@@@@@@@@@@@@@@@@@@  @@@@@  @@      ((( ((( (   (       ((((((((((((((((( @@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@((    (@@@@@@@@@@@@@@@@@@@@@@@@   @((((@@@@      ,        ,, (,      ((((((,,,   ,,((((@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@      @@@@@@@@@@@@@@@@@ ((((@@@@  (   @@@@@@                    ((   (((((  @@@ @@@@ @@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@     @@@@@@@@@@@@@@@@@%@@@,,,,,((((((((%%@@@@                         ,(((( ((@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@      @@@@@@@@@@@@@@    (@@@@@@@(((  @ ((((( (@@                          (((((   @@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@    (@@@@@@@@@%(      @(@@@@@%((((((,(,,,(@@@@@@@@@                          ,,   (@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@     @@@@@@(        (  ((@@@@(((((((   ((((  @@@ (@@@(       ((   (      (     (      @@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@ ,  @@@%(,    ,,((((   ((%@@@@(((%% ,(((((((    %%(%((((%,,     ,  ,,   ,(,,,,,(      @@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@    @(     (((((((    (  (((@@@@@@   (((((( (( ((((((((((( (      (  ( ((     (       @@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@ ,,,  ,,((((((((( ,((((((,,,,((%@((((,(,      ((((((((((((((,         (,              @@@@@@@(    (@@@@@@@
// @@@@@@@@@@@@((    (((((((((((  (((((((((        ((        ((((((((((((((((((                       @@@@@@@  @((  @@@@@@@
// @@@@@@@@@%%,  ,((((((((((((( ,((((,,,(((((((            ,((((((((((((((((((((                    ((@@@@@@ (,,%,( (@@@@@@
// @@@@@@@@@   ((((((((((((((  (     @@@@@@ ((((((        ((((((((((((((((((( @@                    @@@@@@@@   ( ( (  @@@@@
// @@@@@%,   ,%(@@    @@@(((  , (@@@@@@@,(((((((((,((((  (((((((,,,(((((,,,,%@%((            ,(((%@@@@@@@@@@(       ,  @@@@
// @@@@@( @@@@@@@@@    @@@( @@@@@@@@@@ ((((((((((((((((( ((((((((((((((((@@@(((((((  ((((((((( @@@@@@@@@@@@@@   (      @@@@
// @@,(@@@@@@@@@@@@@(    ((@@@@@@@,(   (((((((((((,((( (,((  ,(((((((((((%@%((((((((((((((,,@@(  @@@@@@@@@@( ,,  ,,,   @@@@
// @@@@@@@@@@@@@@@@@@@                 ((((((((((( ((( ( (((( ((((((((((((( @@((((((((((   @@@@@@@@@@@@@@@  ( ( (  (   @@@@
// @@@@@@@@@@@@@@@@@@@@@@(          ( ( (((((((((( ((((((,,((((, ,(((((,((, ,,(@(,,(,,     (@@@@@@@@@@@@@@(%   %,% @,   @@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   ( (((((((((  (((((  (((((     (((  (((((((@@           @@@@@@@@@@@@@@    @ ( @@ @ @@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(  ((((((((((      ,,,    ((((((((( , (,,(( ((%@   ((        ((@@@@@(       @@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@((( ((((( (((  @@@@@@@@        @@@@@@ (((  ( ((@ @@@@@@@                  @@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@((((  ,(((    ((@@@((         (@@@@@@@(,(,   , @(@@@@@@@@@@@((((     ( ((((@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (((      @@@@@@@@ (       @@@@@@@@@@@         @@@@@@@@@@@@@@@@@( @@@(@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ ,((((   (@@@@@@@@@@,((,     @@@@@@@@@  ,(,,( , (@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (((   (   @@@@@@@@@@@(( ((      @@@@  (((((((( (   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((( ,,(( (( ,  (     ((( ,,,,,            ,, ,,,, ,,  (((((((@@@@@@@@((((@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@                                                                                      @@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@(((((((@@( @@@((((((((((((((((((@@((           (((((      ((@@(((@@((((@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//
// Forgotten Runes Beasts
// https://forgottenrunes.com
// Twitter: @forgottenrunes
//
// SPDX-License-Identifier: GPL-3.0
/// @title The Beasts DAO auction house
//
// LICENSE
// ForgottenRunesBeastsAuctionHouse.sol is a modified version of
// NounsAuctionHouse.sol which, in turn, is a modified version of Zora's AuctionHouse.sol:
// https://github.com/ourzora/auction-house/blob/54a12ec1a6cf562e49f0a4917990474b11350a2d/contracts/AuctionHouse.sol
// NounsAuctionHouse.sol source code Copyright Nouns DAO licensed under the GPL-3.0 license.
// AuctionHouse.sol source code Copyright Zora licensed under the GPL-3.0 license.
// ForgottenRunesBeastsAuctionHouse.sol modifications by Dotta
pragma solidity ^0.8.6;

import {Pausable} from '@openzeppelin/contracts/security/Pausable.sol';
import {ReentrancyGuard} from '@openzeppelin/contracts/security/ReentrancyGuard.sol';
import {Ownable} from '@openzeppelin/contracts/access/Ownable.sol';
import {IERC20} from '@openzeppelin/contracts/token/ERC20/IERC20.sol';
import {IBeastsAuctionHouse} from './interfaces/IBeastsAuctionHouse.sol';
import {IBeastsToken} from './interfaces/IBeastsToken.sol';
import {IWETH} from '../interfaces/IWETH.sol';
import 'hardhat/console.sol';

contract ForgottenRunesBeastsAuctionHouse is
    IBeastsAuctionHouse,
    Pausable,
    ReentrancyGuard,
    Ownable
{
    /// @notice Address of the contract which can open the gate e.g. GateToTheSeventhRealmMinter
    address public opener;

    /// @notice The Beasts ERC721 token contract
    IBeastsToken public beasts;

    /// @notice The address of where to send the funds when an auction is settled
    address public treasury;

    /// @notice The address of the WETH contract
    address public weth;

    /// @notice The minimum amount of time left in an auction after a new bid is created
    uint256 public timeBuffer;

    /// @notice The minimum price accepted in an auction
    uint256 public reservePrice;

    /// @notice The minimum percentage difference between the last bid amount and the current bid
    uint8 public minBidIncrementPercentage;

    /// @notice The duration of a single auction
    uint256 public duration;

    /// @notice Flag for being initialized
    bool public initialized;

    /// @notice The active auction
    IBeastsAuctionHouse.Auction public auction;

    constructor() {
        treasury = msg.sender;
        _pause();
    }

    /**
     * @notice Initialize the auction house and base contracts,
     * populate configuration values, and pause the contract.
     */
    function initialize(
        IBeastsToken _beasts,
        address _opener,
        address _weth,
        uint256 _timeBuffer,
        uint256 _reservePrice,
        uint8 _minBidIncrementPercentage,
        uint256 _duration
    ) external whenPaused onlyOwner {
        require(!initialized, 'Already initialized');
        beasts = _beasts;
        opener = _opener;
        weth = _weth;
        timeBuffer = _timeBuffer;
        reservePrice = _reservePrice;
        minBidIncrementPercentage = _minBidIncrementPercentage;
        duration = _duration;
        initialized = true;
    }

    /**
     * @notice Settle the current auction, mint a new Beast, and put it up for auction.
     */
    function settleCurrentAndCreateNewAuction()
        external
        override
        nonReentrant
        whenNotPaused
    {
        _settleAuction();
        _createAuction();
    }

    /**
     * @notice Settle the current auction.
     * @dev This function can only be called when the contract is paused.
     */
    function settleAuction() external override whenPaused nonReentrant {
        _settleAuction();
    }

    /**
     * @notice Create a bid for a Beast, with a given amount.
     * @dev This contract only accepts payment in ETH.
     */
    function createBid(uint256 beastId) external payable override nonReentrant {
        IBeastsAuctionHouse.Auction memory _auction = auction;

        require(_auction.beastId == beastId, 'Beast not up for auction');
        require(block.timestamp < _auction.endTime, 'Auction expired');
        require(msg.value >= reservePrice, 'Must send at least reservePrice');
        require(
            msg.value >=
                _auction.amount +
                    ((_auction.amount * minBidIncrementPercentage) / 100),
            'Must send more than last bid by minBidIncrementPercentage amount'
        );

        address payable lastBidder = _auction.bidder;

        // Refund the last bidder, if applicable
        if (lastBidder != address(0)) {
            _safeTransferETHWithFallback(lastBidder, _auction.amount);
        }

        auction.amount = msg.value;
        auction.bidder = payable(msg.sender);

        // Extend the auction if the bid was received within `timeBuffer` of the auction end time
        bool extended = _auction.endTime - block.timestamp < timeBuffer;
        if (extended) {
            auction.endTime = _auction.endTime = block.timestamp + timeBuffer;
        }

        emit AuctionBid(_auction.beastId, msg.sender, msg.value, extended);

        if (extended) {
            emit AuctionExtended(_auction.beastId, _auction.endTime);
        }
    }

    /**
     * @notice Pause the Beasts auction house.
     * @dev This function can only be called by the owner when the
     * contract is unpaused. While no new auctions can be started when paused,
     * anyone can settle an ongoing auction.
     */
    function pause() external override onlyOwner {
        _pause();
    }

    /**
     * @notice Unpause the Beasts auction house.
     * @dev This function can only be called by the owner when the
     * contract is paused. If required, this function will start a new auction.
     */
    function unpause() external override onlyOwner {
        _unpause();

        if (auction.startTime == 0 || auction.settled) {
            _createAuction();
        }
    }

    /**
     * @notice Unpause the Beasts auction house.
     * @dev This function can only be called by the opener when the
     * contract is paused. If required, this function will start a new auction.
     */
    function openTheGate() external override whenPaused nonReentrant {
        require(_msgSender() == opener, 'Not the opener');
        _unpause();

        if (auction.startTime == 0 || auction.settled) {
            _createAuction();
        }
    }

    /**
     * @notice Set the auction time buffer.
     * @dev Only callable by the owner.
     */
    function setTimeBuffer(uint256 _timeBuffer) external override onlyOwner {
        timeBuffer = _timeBuffer;

        emit AuctionTimeBufferUpdated(_timeBuffer);
    }

    /**
     * @notice Set the auction reserve price.
     * @dev Only callable by the owner.
     */
    function setReservePrice(uint256 _reservePrice)
        external
        override
        onlyOwner
    {
        reservePrice = _reservePrice;

        emit AuctionReservePriceUpdated(_reservePrice);
    }

    /**
     * @notice Set the auction minimum bid increment percentage.
     * @dev Only callable by the owner.
     */
    function setMinBidIncrementPercentage(uint8 _minBidIncrementPercentage)
        external
        override
        onlyOwner
    {
        minBidIncrementPercentage = _minBidIncrementPercentage;

        emit AuctionMinBidIncrementPercentageUpdated(
            _minBidIncrementPercentage
        );
    }

    /**
     * @notice Set the opener
     * @dev Only callable by the owner.
     */
    function setOpener(address _opener) external onlyOwner {
        opener = _opener;
    }

    /**
     * @notice Set the opener
     * @dev Only callable by the owner.
     */
    function setTreasury(address _treasury) external override onlyOwner {
        treasury = _treasury;
    }

    /**
     * @notice Create an auction.
     * @dev Store the auction details in the `auction` state variable and emit an AuctionCreated event.
     * If the mint reverts, the minter was updated without pausing this contract first. To remedy this,
     * catch the revert and pause this contract.
     */
    function _createAuction() internal {
        try beasts.mint(address(this)) returns (uint256 beastId) {
            uint256 startTime = block.timestamp;
            uint256 endTime = startTime + duration;

            auction = Auction({
                beastId: beastId,
                amount: 0,
                startTime: startTime,
                endTime: endTime,
                bidder: payable(0),
                settled: false
            });

            emit AuctionCreated(beastId, startTime, endTime);
        } catch Error(string memory message) {
            console.log('%s', message);
            _pause();
        }
    }

    /**
     * @notice Settle an auction, finalizing the bid and paying out to the owner.
     * @dev If there are no bids, the Beast is burned.
     */
    function _settleAuction() internal {
        IBeastsAuctionHouse.Auction memory _auction = auction;

        require(_auction.startTime != 0, "Auction hasn't begun");
        require(!_auction.settled, 'Auction has already been settled');
        require(
            block.timestamp >= _auction.endTime,
            "Auction hasn't completed"
        );

        auction.settled = true;

        if (_auction.bidder == address(0)) {
            beasts.transferFrom(address(this), owner(), _auction.beastId);
        } else {
            beasts.transferFrom(
                address(this),
                _auction.bidder,
                _auction.beastId
            );
        }

        if (_auction.amount > 0) {
            _safeTransferETHWithFallback(treasury, _auction.amount);
        }

        emit AuctionSettled(_auction.beastId, _auction.bidder, _auction.amount);
    }

    /**
     * @notice Transfer ETH. If the ETH transfer fails, wrap the ETH and try send it as WETH.
     */
    function _safeTransferETHWithFallback(address to, uint256 amount) internal {
        if (!_safeTransferETH(to, amount)) {
            IWETH(weth).deposit{value: amount}();
            IERC20(weth).transfer(to, amount);
        }
    }

    /**
     * @notice Transfer ETH and return the success status.
     * @dev This function only forwards 30,000 gas to the callee.
     */
    function _safeTransferETH(address to, uint256 value)
        internal
        returns (bool)
    {
        (bool success, ) = to.call{value: value, gas: 30_000}(new bytes(0));
        return success;
    }

    /**
     * @notice Conform to safe transfer protocol
     */
    function onERC721Received(
        address,
        address,
        uint256,
        bytes calldata
    ) external pure returns (bytes4) {
        return
            bytes4(
                keccak256('onERC721Received(address,address,uint256,bytes)')
            );
    }
}
