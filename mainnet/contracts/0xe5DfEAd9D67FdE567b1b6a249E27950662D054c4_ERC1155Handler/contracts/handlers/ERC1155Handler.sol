pragma solidity 0.6.12;
pragma experimental ABIEncoderV2;

import "../interfaces/IDepositExecute.sol";
import "./HandlerHelpers.sol";
import "../ERC1155Safe.sol";
import "@openzeppelin/contracts/introspection/ERC165Checker.sol";
import "@openzeppelin/contracts/token/ERC1155/IERC1155MetadataURI.sol";

/**
    @title Handles ERC1155 deposits and deposit executions.
    @author ChainSafe Systems.
    @notice This contract is intended to be used with the Bridge contract.
 */
contract ERC1155Handler is IDepositExecute, HandlerHelpers, ERC1155Safe {
    using ERC165Checker for address;

    bytes4 private constant _INTERFACE_ERC1155_METADATA = 0x0e89341c;

    struct DepositRecord {
        address _tokenAddress;
        uint8   _destinationChainID;
        bytes32 _resourceID;
        bytes   _destinationRecipientAddress;
        address _depositer;
        uint    _tokenID;
        uint    _amount;
        bytes   _extraData;
        bytes   _metaData;
    }

    // destId => depositNonce => Deposit Record
    mapping (uint8 => mapping (uint64 => DepositRecord)) public _depositRecords;

    /**
        @param bridgeAddress Contract address of previously deployed Bridge.
        @param initialResourceIDs Resource IDs are used to identify a specific contract address.
        These are the Resource IDs this contract will initially support.
        @param initialContractAddresses These are the addresses the {initialResourceIDs} will point to, and are the contracts that will be
        called to perform various deposit calls.
        @param burnableContractAddresses These addresses will be set as burnable and when {deposit} is called, the deposited token will be burned.
        When {executeProposal} is called, new tokens will be minted.

        @dev {initialResourceIDs} and {initialContractAddresses} must have the same length (one resourceID for every address).
        Also, these arrays must be ordered in the way that {initialResourceIDs}[0] is the intended resourceID for {initialContractAddresses}[0].
     */
    constructor(
        address bridgeAddress,
        bytes32[] memory initialResourceIDs,
        address[] memory initialContractAddresses,
        address[] memory burnableContractAddresses
    ) public {
        require(initialResourceIDs.length == initialContractAddresses.length,
            "initialResourceIDs and initialContractAddresses len mismatch");

        _bridgeAddress = bridgeAddress;

        for (uint256 i = 0; i < initialResourceIDs.length; i++) {
            _setResource(initialResourceIDs[i], initialContractAddresses[i]);
        }

        for (uint256 i = 0; i < burnableContractAddresses.length; i++) {
            _setBurnable(burnableContractAddresses[i]);
        }
    }

    /**
        @param depositNonce This ID will have been generated by the Bridge contract.
        @param destId ID of chain deposit will be bridged to.
        @return DepositRecord which consists of:
        - _tokenAddress Address used when {deposit} was executed.
        - _destinationChainID ChainID deposited tokens are intended to end up on.
        - _resourceID ResourceID used when {deposit} was executed.
        - _destinationRecipientAddress Address tokens are intended to be deposited to on desitnation chain.
        - _depositer Address that initially called {deposit} in the Bridge contract.
        - _tokenID ID of ERC1155.
        - _amount Amount of tokenID to transfer.
        - _extraData Optional data field supported by ERC1155
    */
    function getDepositRecord(uint64 depositNonce, uint8 destId) external view returns (DepositRecord memory) {
        return _depositRecords[destId][depositNonce];
    }

    /**
        @notice A deposit is initiatied by making a deposit in the Bridge contract.
        @param destinationChainID Chain ID of chain token is expected to be bridged to.
        @param depositNonce This value is generated as an ID by the Bridge contract.
        @param depositer Address of account making the deposit in the Bridge contract.
        @param data Consists of: {resourceID}, {tokenID}, {lenDestinationRecipientAddress},
        and {destinationRecipientAddress} all padded to 32 bytes.
        @notice Data passed into the function should be constructed as follows:
        tokenID                                     uint256    bytes    0  - 32
        amount                                      uint256    bytes    32 - 64
        destinationRecipientAddress     length      uint256    bytes    64 - 96
        extraData                       length      uint256    bytes    96 - 128
        destinationRecipientAddress                   bytes    bytes    128 - x where x = (128 + len(destinationRecipientAddress))
        extraData                                     bytes    bytes    x  - (x + len(extraData))

        @dev Depending if the corresponding {tokenAddress} for the parsed {resourceID} is
        marked true in {_burnList}, deposited tokens will be burned, if not, they will be locked.
     */
    function deposit(bytes32    resourceID,
                    uint8       destinationChainID,
                    uint64      depositNonce,
                    address     depositer,
                    bytes       calldata data
                    ) external override onlyBridge {
        // payload data length values
        uint         lenDestinationRecipientAddress;
        uint         lenExtraData;

        // intermediate values
        uint         byteOffset;

        // Create a new record and use it for storage becuase, otherwise,
        // we'd have too many local variables and the compiler would complain. 
        DepositRecord memory record = DepositRecord(
            _resourceIDToTokenContractAddress[resourceID], // _tokenAddress
            uint8(destinationChainID),
            resourceID,
            hex'',      // _destinationRecipientAddress
            depositer,  // _depositer
            0,          // _tokenID
            0,          // _amount
            hex'',      // _extraData
            hex''       // _metaData
        );

        require(_contractWhitelist[record._tokenAddress], "provided tokenAddress is not whitelisted");

        // Note that token ID and amount are both 32 bytes, so don't need extra length parameters.
        (record._tokenID, record._amount, lenDestinationRecipientAddress, lenExtraData) = abi.decode(data, (uint, uint, uint, uint));

        byteOffset = 128;
        record._destinationRecipientAddress = bytes(data[byteOffset:byteOffset + lenDestinationRecipientAddress]);

        byteOffset += lenDestinationRecipientAddress;
        record._extraData = bytes(data[byteOffset:byteOffset + lenExtraData]);

        // Check if the contract supports metadata, fetch it if it does
        if (record._tokenAddress.supportsInterface(_INTERFACE_ERC1155_METADATA)) {
            IERC1155MetadataURI erc1155 = IERC1155MetadataURI(record._tokenAddress);
            record._metaData = bytes(erc1155.uri(record._tokenID));
        }

        if (_burnList[record._tokenAddress]) {
            burnERC1155(record._tokenAddress, record._depositer, record._tokenID, record._amount);
        } else {
            lockERC1155(record._tokenAddress, record._depositer, address(this), record._tokenID, record._amount, record._extraData);
        }

        _depositRecords[destinationChainID][depositNonce] = record;
    }

    /**
        @notice Proposal execution should be initiated when a proposal is finalized in the Bridge contract.
        by a relayer on the deposit's destination chain.
        @param data Consists of {tokenID}, {resourceID}, {lenDestinationRecipientAddress}, {lenExtraData}
        {destinationRecipientAddress}, and {extraData} all padded to 32 bytes.
        @notice Data passed into the function should be constructed as follows:
        tokenID                                     uint256    bytes    0   - 32
        amount                                      uint256    bytes    32  - 64
        destinationRecipientAddress     length      uint256    bytes    64  - 96
        extraData                       length      uint256    bytes    96  - 128
        destinationRecipientAddress                   bytes    bytes    128 - x where x = (128 + len(destinationRecipientAddress))
        extraData                                     bytes    bytes    x   - y where y = (x + len(extraData))
        metaData                        length      uint256    bytes    y   - z where z = y + 32
        metaData                                      bytes    bytes    z   - a where a = (z + len(metaData))
     */
    function executeProposal(bytes32 resourceID, bytes calldata data) external override onlyBridge {
        // Create a new record and use it for storage becuase, otherwise,
        // we'd have too many local variables and the compiler would complain. 
        DepositRecord memory record = DepositRecord(
            address(0), // _tokenAddress
            0,          // _destinationChainId
            resourceID, // _resourceID
            hex'',      // _destinationRecipientAddress
            address(0), // _depositor (not used in this function)
            0,          // _tokenID
            0,          // _amount
            hex'',      // _extraData
            hex''       // _metaData
        );

        // payload
        uint         lenDestinationRecipientAddress;
        uint         lenExtraData;
        uint         lenMetaData;

        // intermediate variables
        uint         byteOffset;

        (record._tokenID, record._amount, lenDestinationRecipientAddress, lenExtraData) = abi.decode(data, (uint, uint, uint, uint));

        byteOffset = 128;
        record._destinationRecipientAddress = bytes(data[byteOffset:byteOffset + lenDestinationRecipientAddress]);

        byteOffset += lenDestinationRecipientAddress;
        record._extraData = bytes(data[byteOffset:byteOffset + lenExtraData]);

        byteOffset += lenExtraData;
        bytes memory lenMetaDataBytes = bytes(data[byteOffset:byteOffset + 32]);

        assembly {
            lenMetaData := mload(add(lenMetaDataBytes, 0x20))
        }

        byteOffset += 32;
        record._metaData = bytes(data[byteOffset:byteOffset + lenMetaData]);

        bytes20 recipientAddress;
        bytes memory destinationRecipientAddress = record._destinationRecipientAddress;

        assembly {
            recipientAddress := mload(add(destinationRecipientAddress, 0x20))
        }

        record._tokenAddress = _resourceIDToTokenContractAddress[resourceID];
        require(_contractWhitelist[address(record._tokenAddress)], "provided tokenAddress is not whitelisted");

        if (_burnList[record._tokenAddress]) {
            mintERC1155(record._tokenAddress, address(recipientAddress), record._tokenID, record._amount, record._metaData, record._extraData);
        } else {
            releaseERC1155(record._tokenAddress, address(this), address(recipientAddress), record._tokenID, record._amount, record._extraData);
        }
    }

    /**
        @notice Used to manually release ERC1155 tokens from ERC1155Safe.
        @param tokenAddress Address of token contract to release.
        @param recipient Address to release token to.
        @param tokenId The ERC1155 token ID to release. Amount is fixed to 1
     */
    function withdraw(address tokenAddress, address recipient, uint256 tokenId) external override onlyBridge {
        ERC1155 erc1155Token = ERC1155(tokenAddress);
        releaseERC1155(tokenAddress, address(this), recipient, tokenId, erc1155Token.balanceOf(address(this), tokenId), bytes(""));
    }
}
