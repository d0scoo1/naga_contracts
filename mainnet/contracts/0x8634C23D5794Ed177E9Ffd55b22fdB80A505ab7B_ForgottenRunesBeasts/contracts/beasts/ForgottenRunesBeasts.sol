// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@%@@@@@@@@@@@@@@@@@@@@@@(((        ((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                 ( @@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@((                             (@@@@@            ,         @@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@         @@@@@@@@@@@@@@@@@@@@@@       (((    ((((((      ((((((  @@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@((     (((@@@@@@@@@@@@@@@@@@@@@@@@@@(,((,       ,,,,,       ((((%(%(,(@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@       @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((                       (((@@@@(((   @@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@       (@@@@@@@@@@@@@@@@@@@@@@(( @@@@@@@( , ,(   ,  ((   ,           ,,(((((((((, @@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@       @@@@@@@@@@@@@@@@@@@@@@@  @@@@@  @@      ((( ((( (   (       ((((((((((((((((( @@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@((    (@@@@@@@@@@@@@@@@@@@@@@@@   @((((@@@@      ,        ,, (,      ((((((,,,   ,,((((@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@      @@@@@@@@@@@@@@@@@ ((((@@@@  (   @@@@@@                    ((   (((((  @@@ @@@@ @@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@     @@@@@@@@@@@@@@@@@%@@@,,,,,((((((((%%@@@@                         ,(((( ((@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@      @@@@@@@@@@@@@@    (@@@@@@@(((  @ ((((( (@@                          (((((   @@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@    (@@@@@@@@@%(      @(@@@@@%((((((,(,,,(@@@@@@@@@                          ,,   (@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@     @@@@@@(        (  ((@@@@(((((((   ((((  @@@ (@@@(       ((   (      (     (      @@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@ ,  @@@%(,    ,,((((   ((%@@@@(((%% ,(((((((    %%(%((((%,,     ,  ,,   ,(,,,,,(      @@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@    @(     (((((((    (  (((@@@@@@   (((((( (( ((((((((((( (      (  ( ((     (       @@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@ ,,,  ,,((((((((( ,((((((,,,,((%@((((,(,      ((((((((((((((,         (,              @@@@@@@(    (@@@@@@@
// @@@@@@@@@@@@((    (((((((((((  (((((((((        ((        ((((((((((((((((((                       @@@@@@@  @((  @@@@@@@
// @@@@@@@@@%%,  ,((((((((((((( ,((((,,,(((((((            ,((((((((((((((((((((                    ((@@@@@@ (,,%,( (@@@@@@
// @@@@@@@@@   ((((((((((((((  (     @@@@@@ ((((((        ((((((((((((((((((( @@                    @@@@@@@@   ( ( (  @@@@@
// @@@@@%,   ,%(@@    @@@(((  , (@@@@@@@,(((((((((,((((  (((((((,,,(((((,,,,%@%((            ,(((%@@@@@@@@@@(       ,  @@@@
// @@@@@( @@@@@@@@@    @@@( @@@@@@@@@@ ((((((((((((((((( ((((((((((((((((@@@(((((((  ((((((((( @@@@@@@@@@@@@@   (      @@@@
// @@,(@@@@@@@@@@@@@(    ((@@@@@@@,(   (((((((((((,((( (,((  ,(((((((((((%@%((((((((((((((,,@@(  @@@@@@@@@@( ,,  ,,,   @@@@
// @@@@@@@@@@@@@@@@@@@                 ((((((((((( ((( ( (((( ((((((((((((( @@((((((((((   @@@@@@@@@@@@@@@  ( ( (  (   @@@@
// @@@@@@@@@@@@@@@@@@@@@@(          ( ( (((((((((( ((((((,,((((, ,(((((,((, ,,(@(,,(,,     (@@@@@@@@@@@@@@(%   %,% @,   @@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   ( (((((((((  (((((  (((((     (((  (((((((@@           @@@@@@@@@@@@@@    @ ( @@ @ @@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(  ((((((((((      ,,,    ((((((((( , (,,(( ((%@   ((        ((@@@@@(       @@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@((( ((((( (((  @@@@@@@@        @@@@@@ (((  ( ((@ @@@@@@@                  @@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@((((  ,(((    ((@@@((         (@@@@@@@(,(,   , @(@@@@@@@@@@@((((     ( ((((@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (((      @@@@@@@@ (       @@@@@@@@@@@         @@@@@@@@@@@@@@@@@( @@@(@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ ,((((   (@@@@@@@@@@,((,     @@@@@@@@@  ,(,,( , (@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (((   (   @@@@@@@@@@@(( ((      @@@@  (((((((( (   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((( ,,(( (( ,  (     ((( ,,,,,            ,, ,,,, ,,  (((((((@@@@@@@@((((@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@                                                                                      @@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@(((((((@@( @@@((((((((((((((((((@@((           (((((      ((@@(((@@((((@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//
// Forgotten Runes Beasts
// https://forgottenrunes.com
// Twitter: @forgottenrunes
pragma solidity ^0.8.0;

import '../util/Ownablearama.sol';
import '@openzeppelin/contracts/token/ERC721/ERC721.sol';
import '@openzeppelin/contracts/security/ReentrancyGuard.sol';

/**
 * @dev This implements the Beasts of the Runiverse. The Beasts are {ERC721} tokens.
 */
contract ForgottenRunesBeasts is Ownablearama, ReentrancyGuard, ERC721 {
    using Strings for uint256;

    /// @notice Counter to track the number minted so far
    uint256 public numMinted = 0;

    /// @notice Beasts are rare, but the number that exist is not fully known
    uint256 public mintableSupply = 7;

    /// @notice Address of the minter
    address public minter;

    /// @notice The base URI for the metadata of the tokens
    string public baseTokenURI;

    string public constant R =
        'And I stood upon the sand of the sea, and saw a beast rise up out of the sea, having seven heads and ten horns, and upon his horns ten crowns, and upon his heads the name of blasphemy';

    /**
     * @dev Create the contract and set the initial baseURI
     * @param baseURI string the initial base URI for the token metadata URL
     */
    constructor(string memory baseURI)
        ERC721('ForgottenRunesBeasts', 'BEASTS')
    {
        setBaseURI(baseURI);
    }

    /**
     * @dev Convenient way to initialize the contract
     * @param newMinter address of the minter contract
     */
    function initialize(address newMinter) public onlyOwner {
        setMinter(newMinter);
    }

    /**
     * @dev Returns the URL of a given tokenId
     * @param tokenId uint256 ID of the token to be minted
     * @return string the URL of a given tokenId
     */
    function tokenURI(uint256 tokenId)
        public
        view
        virtual
        override
        returns (string memory)
    {
        require(
            _exists(tokenId),
            'ERC721Metadata: URI query for nonexistent token'
        );

        return string(abi.encodePacked(baseTokenURI, tokenId.toString()));
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return baseTokenURI;
    }

    /**
     * @dev Mint the next token
     * @param recipient address representing the owner of the new tokenId
     * @return tokenId uint256 the new tokenId
     */
    function mint(address recipient) public nonReentrant returns (uint256) {
        require(_msgSender() == minter, 'Not a minter');
        uint256 tokenId = numMinted + 1; // start at 1
        require(tokenId < mintableSupply + 1, 'Not available');
        _safeMint(recipient, tokenId);
        numMinted += 1;
        return tokenId;
    }

    /**
     * @dev Mint a new token
     * @param recipient address representing the owner of the new tokenId
     * @param tokenId uint256 ID of the token to be minted
     */
    function mintTokenId(address recipient, uint256 tokenId)
        public
        nonReentrant
        returns (uint256)
    {
        require(_msgSender() == minter, 'Not a minter');
        require(tokenId < mintableSupply + 1, 'Not available');
        numMinted += 1;
        _safeMint(recipient, tokenId);
        return tokenId;
    }

    function exists(uint256 tokenId) public view returns (bool) {
        return _exists(tokenId);
    }

    /**
     * Only the owner can do these things
     */

    /**
     * @dev Sets the new max supply
     * @param newMintableSupply uint256 the new mintable supply
     */
    function setMintableSupply(uint256 newMintableSupply) public onlyOwner {
        require(newMintableSupply >= mintableSupply, 'must be greater');
        mintableSupply = newMintableSupply;
    }

    /**
     * @dev Sets a new base URI
     * @param newBaseURI string the new token base URI
     */
    function setBaseURI(string memory newBaseURI) public onlyOwner {
        baseTokenURI = newBaseURI;
    }

    /**
     * @dev Sets a new primary minter address
     * @param newMinter address of the new minter
     */
    function setMinter(address newMinter) public onlyOwner {
        minter = newMinter;
    }

    function uploadImage(bytes calldata s) external onlyOwner {}

    function uploadAttributes(bytes calldata s) external onlyOwner {}
}
